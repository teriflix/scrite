/****************************************************************************
**
** Copyright (C) VCreate Logic Pvt. Ltd. Bengaluru
** Author: Prashanth N Udupa (prashanth@scrite.io)
**
** This code is distributed under GPL v3. Complete text of the license
** can be found here: https://www.gnu.org/licenses/gpl-3.0.txt
**
** This file is provided AS IS with NO WARRANTY OF ANY KIND, INCLUDING THE
** WARRANTY OF DESIGN, MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE.
**
****************************************************************************/

import QtQuick 2.15
import QtQuick.Controls 2.15

import io.scrite.components 1.0



import "qrc:/qml/globals"
import "qrc:/qml/controls"

Item {
    id: specialSymbolsSupport
    property Item textEditor
    property bool textEditorHasCursorInterface: false
    property bool includeEmojis: true
    property bool showingSymbols: symbolMenu.visible

    signal symbolSelected(string text)

    EventFilter.target: textEditor
    EventFilter.active: textEditor !== null && specialSymbolsSupport.enabled && textEditor.activeFocus
    EventFilter.events: [6]
    EventFilter.onFilter: {
        if(!specialSymbolsSupport.enabled)
            return

        if(textEditorHasCursorInterface && textEditor.readOnly)
            return

        if(event.key === Qt.Key_F3) {
            symbolMenu.visible = true
            result.filer = true
            result.accepted = true
        }
    }

    VclMenu {
        id: symbolMenu

        width: 514

        focus: false
        autoWidth: false

        VclMenuItem {
            width: symbolMenu.width
            height: 400
            focusPolicy: Qt.NoFocus
            background: Item { }
            contentItem: SpecialSymbolsPanel {
                includeEmojis: specialSymbolsSupport.includeEmojis
                onSymbolClicked: {
                    if(!specialSymbolsSupport.enabled)
                        return

                    if(textEditorHasCursorInterface) {
                        if(textEditor.readOnly)
                            return

                        var cp = textEditor.cursorPosition
                        textEditor.insert(textEditor.cursorPosition, text)
                        Utils.execLater(textEditor, 250, function() { textEditor.cursorPosition = cp + text.length })
                        symbolMenu.close()
                        textEditor.forceActiveFocus()
                    } else {
                        symbolMenu.close()
                        symbolSelected(text)
                    }
                }
            }
        }
    }

    component SpecialSymbolsPanel : Rectangle {
        property bool includeEmojis: true
        readonly property var symbols: [
            {
                "title": "Symbols",
                "symbols": ["Â¡", "Â¢", "Â£", "Â¤", "Â¥", "â‚¬", "Â¦", "Â§", "Â¨", "Â©", "Âª", "Â«", "Â¬", "", "Â®", "Â¯", "Â°", "Â±", "Â²", "Â³", "Â´", "Âµ", "Â¶", "Â·", "Â¸", "Â¹", "Âº", "Â»", "Â¼", "Â½", "Â¾", "Â¿", "Î‘", "Î’", "Î“", "Î”", "Î•", "Î–", "Î—", "Î˜", "Î™", "Îš", "Î›", "Îœ", "Î", "Î", "ÎŸ", "Î ", "Î¡", "Î£", "Î¤", "Î¥", "Î¦", "Î§", "Î¨", "Î©", "Î±", "Î²", "Î³", "Î´", "Îµ", "Î¶", "Î·", "Î¸", "Î¹", "Îº", "Î»", "Î¼", "Î½", "Î¾", "Î¿", "Ï€", "Ï", "Ï‚", "Ïƒ", "Ï„", "Ï…", "Ï†", "Ï‡", "Ïˆ", "Ï‰", "Ï‘", "Ï’", "Ï–", "â€ ", "â€¡", "â€¢", "â€¦", "â€°", "â€²", "â€³", "â€¾", "â„", "â„˜", "â„‘", "â„œ", "â„¢", "â„µ", "â†", "â†‘", "â†’", "â†“", "â†”", "â†µ", "â‡", "â‡‘", "â‡’", "â‡“", "â‡”", "âˆ€", "âˆ‚", "âˆƒ", "âˆ…", "âˆ‡", "âˆˆ", "âˆ‰", "âˆ‹", "âˆ", "âˆ‘", "âˆ’", "âˆ—", "âˆš", "âˆ", "âˆ", "âˆ ", "âˆ§", "âˆ¨", "âˆ©", "âˆª", "âˆ«", "âˆ´", "âˆ¼", "â‰…", "â‰ˆ", "â‰ ", "â‰¡", "â‰¤", "â‰¥", "âŠ‚", "âŠƒ", "âŠ„", "âŠ†", "âŠ‡", "âŠ•", "âŠ—", "âŠ¥", "â‹…", "âŒˆ", "âŒ‰", "âŒŠ", "âŒ‹", "âŸ¨", "âŸ©", "â—Š", "â™ ", "â™£", "â™¥", "â™¦"]
            },
            {
                "title": "Letter",
                "symbols": ["Ã‚", "Ãƒ", "Ã„", "Ã…", "Ã†", "Ã‡", "Ãˆ", "Ã‰", "ÃŠ", "Ã‹", "ÃŒ", "Ã", "Ã", "Ã", "Ã", "Ã‘", "Ã’", "Ã“", "Ã”", "Ã•", "Ã–", "Ã—", "Ã˜", "Ã™", "Ãš", "Ã›", "Ãœ", "Ã", "Ã", "ÃŸ", "Ã ", "Ã¡", "Ã¢", "Ã£", "Ã¤", "Ã¥", "Ã¦", "Ã§", "Ã¨", "Ã©", "Ãª", "Ã«", "Ã¬", "Ã­", "Ã®", "Ã¯", "Ã°", "Ã±", "Ã²", "Ã³", "Ã´", "Ãµ", "Ã¶", "Ã·", "Ã¸", "Ã¹", "Ãº", "Ã»", "Ã¼", "Ã½", "Ã¾", "Ã¿", "Å’", "Å“", "Å ", "Å¡", "Å¸", "Æ’", "Ë†", "Ëœ"]
            },
            {
                "title": "Emoji",
                "symbols": ["ğŸ˜","ğŸ˜‚","ğŸ˜ƒ","ğŸ˜„","ğŸ˜…","ğŸ˜†","ğŸ˜‰","ğŸ˜Š","ğŸ˜‹","ğŸ˜Œ","ğŸ˜","ğŸ˜","ğŸ˜’","ğŸ˜”","ğŸ˜–","ğŸ˜˜","ğŸ˜š","ğŸ˜œ","ğŸ˜","ğŸ˜","ğŸ˜ ","ğŸ˜¡","ğŸ˜¢","ğŸ˜£","ğŸ˜¤","ğŸ˜¥","ğŸ˜¨","ğŸ˜©","ğŸ˜ª","ğŸ˜«","ğŸ˜­","ğŸ˜°","ğŸ˜±","ğŸ˜²","ğŸ˜³","ğŸ˜µ","ğŸ˜·","ğŸ˜‡","ğŸ˜ˆ","ğŸ˜","ğŸ˜","ğŸ˜¶","ğŸ˜¸","ğŸ˜¹","ğŸ˜º","ğŸ˜»","ğŸ˜¼","ğŸ˜½","ğŸ˜¾","ğŸ˜¿","ğŸ™€","ğŸ™…","ğŸ™†","ğŸ™‡","ğŸ™ˆ","ğŸ™‰","ğŸ™Š","ğŸ™‹","ğŸ™Œ","ğŸ™","ğŸ™","ğŸ™"]
            }
        ]

        signal symbolClicked(string text)

        id: symbolsView
        width: 500; height: 400
        color: Runtime.colors.primary.c100.background

        Rectangle {
            id: symbolsPanel

            property int currentIndex: 0
            property bool currentIndexIsEmoji: symbols[symbolsPanel.currentIndex].title === "Emoji"

            anchors.top: parent.top
            anchors.left: parent.left
            anchors.bottom: parent.bottom

            width: 100
            color: Runtime.colors.primary.c700.background

            Column {
                width: parent.width

                Repeater {
                    model: symbols

                    delegate: Rectangle {
                        required property int index
                        required property var modelData

                        width: symbolsPanel.width
                        height: 40
                        color: symbolsPanel.currentIndex === index ? Runtime.colors.primary.windowColor : Qt.rgba(0,0,0,0)

                        VclLabel {
                            anchors.verticalCenter: parent.verticalCenter
                            anchors.right: parent.right
                            anchors.rightMargin: 10
                            text: modelData.title
                            font.pointSize: Runtime.idealFontMetrics.font.pointSize
                            color: symbolsPanel.currentIndex === index ? "black" : "white"
                        }

                        MouseArea {
                            anchors.fill: parent
                            onClicked: symbolsPanel.currentIndex = index
                        }
                    }
                }
            }
        }

        GridView {
            id: symbolsGridView

            ScrollBar.vertical: VclScrollBar { flickable: symbolsGridView }

            anchors.top: parent.top
            anchors.left: symbolsPanel.right
            anchors.right: parent.right
            anchors.bottom: parent.bottom
            anchors.leftMargin: 5

            clip: true
            rightMargin: 14

            cellWidth: symbolsPanel.currentIndexIsEmoji ? 50 : 40
            cellHeight: cellWidth
            model: symbols[symbolsPanel.currentIndex].symbols
            header: Item {
                width: symbolsGridView.width-14
                height: symbolsPanel.currentIndexIsEmoji ? 35 : 0

                VclLabel {
                    visible: symbolsPanel.currentIndexIsEmoji
                    width: parent.width
                    horizontalAlignment: Text.AlignHCenter
                    anchors.centerIn: parent
                    font.pointSize: Runtime.idealFontMetrics.font.pointSize
                    text: includeEmojis ? "Emojis may not be included in PDF exports." : "Emojis are not supported in this text area."
                }
            }

            delegate: Item {
                required property int index
                required property string modelData

                width: symbolsGridView.cellWidth
                height: symbolsGridView.cellHeight
                enabled: !symbolsPanel.currentIndexIsEmoji || includeEmojis
                opacity: enabled ? 1 : 0.5

                Rectangle {
                    anchors.fill: parent
                    anchors.margins: 1
                    border.width: 1
                    border.color: Runtime.colors.primary.borderColor
                    opacity: 0.5
                }

                VclText {
                    anchors.centerIn: parent
                    text: modelData
                    font.pixelSize: parent.height * 0.6
                }

                MouseArea {
                    anchors.fill: parent
                    onClicked: symbolClicked(modelData)
                }
            }
        }
    }
}
